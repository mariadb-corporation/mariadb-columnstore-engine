cmake_minimum_required(VERSION @CMAKE_VERSION@)

include(ExternalProject)

if(CMAKE_CXX_COMPILER_ID MATCHES "GNU")
  set(_toolset "gcc")
elseif(CMAKE_CXX_COMPILER_ID MATCHES ".*Clang")
  set(_toolset "clang")
elseif(CMAKE_CXX_COMPILER_ID MATCHES "Intel")
  set(_toolset "intel-linux")
endif()

set(_b2args link=shared;threading=multi;variant=release;toolset=${_toolset};--with-system;--with-filesystem;--with-thread;--with-regex;--with-date_time)

ExternalProject_Add(boost
  PREFIX build
  URL https://sourceforge.net/projects/boost/files/boost/1.55.0/boost_1_55_0.zip
  URL_HASH SHA256=ae85620e810b87a03e1acf8bbf0d4ad87c0cf7040cf6a4e1d8958488ebe42e7e
  DOWNLOAD_NO_PROGRESS TRUE
  UPDATE_COMMAND ""
  CONFIGURE_COMMAND <SOURCE_DIR>/bootstrap.sh 
    --with-toolset=${_toolset}
    --prefix=${CMAKE_CURRENT_SOURCE_DIR}/../boost
	--with-libraries=system,filesystem,thread,regex,date_time
  BUILD_COMMAND <SOURCE_DIR>/b2 -q ${_b2args}
  LOG_BUILD TRUE
  BUILD_IN_SOURCE TRUE
  INSTALL_COMMAND <SOURCE_DIR>/b2 -q install ${_b2args}
  LOG_INSTALL TRUE
)

unset(_b2args)
unset(_toolset)